# Main entrypoint of the workflow. 
# Please follow the best practices: 
# https://snakemake.readthedocs.io/en/stable/snakefiles/best_practices.html,
# in particular regarding the standardized folder structure mentioned there. 

conda: local("envs/global.yaml")  # Make conda env accessible workflow-wide

configfile: local("config/config.yml")

SUBSETS = config["subset"]
BASELINE_DATE = config["pmc_baseline_date"]

checkpoint bulk_download_pmc_metadata:
    input:
        storage.ftp(
            "ftp://ftp.ncbi.nlm.nih.gov/pub/pmc/oa_bulk/oa_noncomm/xml/oa_noncomm_xml.PMC"
            f"{{subset}}"
            "xxxxxx.baseline."
            f"{BASELINE_DATE}"
            ".tar.gz"
        ),
        storage.ftp(
            "ftp://ftp.ncbi.nlm.nih.gov/pub/pmc/oa_bulk/oa_comm/xml/oa_comm_xml.PMC"
            f"{{subset}}"
            "xxxxxx.baseline."
            f"{BASELINE_DATE}"
            ".tar.gz"
        )
    output:
        f"resources/metadata/oa_noncomm_xml.PMC{{subset}}xxxxxx.baseline.{BASELINE_DATE}.tar.gz",
        f"resources/metadata/oa_comm_xml.PMC{{subset}}xxxxxx.baseline.{BASELINE_DATE}.tar.gz"
    threads: 1
    resources:
        runtime="1-12:00:00"
    shell:
        "mkdir -p resources/metadata && cp {input} resources/metadata"

rule extract_archives:
    input:
        f"resources/metadata/oa_noncomm_xml.PMC{{subset}}xxxxxx.baseline.{BASELINE_DATE}.tar.gz",
        f"resources/metadata/oa_comm_xml.PMC{{subset}}xxxxxx.baseline.{BASELINE_DATE}.tar.gz"
    output:
        directory("resources/metadata/PMC{subset}xxxxxx/")
    resources:
        runtime="06:00:00"
    run:
        shell(f"for FILE in {{input}}; do tar -xzvf $FILE -C resources/metadata; done")

rule write_luts:
    input:
        "resources/metadata/PMC{subset}xxxxxx/"
    output:
        "results/PMC{subset}.json"
    resources:
        runtime="06:00:00"
    log:
        "logs/{subset}.log"
    script:
        "scripts/write_lut.py"


rule all:
    default_target: True
    input:
        expand("results/PMC{subset}.json", subset=SUBSETS)

rule clean:
    threads: 1
    shell:
        "rm -rf resources/ results/ .cache/ .conda/ .snakemake/ logs/"  # Keeping extracted files
